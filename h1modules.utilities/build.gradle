plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:7.1.0.202411261347-r'
    implementation 'org.eclipse.platform:org.eclipse.core.resources:3.22.0'
    implementation 'org.eclipse.jdt:org.eclipse.jdt.core:3.40.0'
    implementation 'org.eclipse.jdt:org.eclipse.jdt.launching:3.23.100'
    implementation ('org.eclipse.platform:org.eclipse.swt.win32.win32.x86_64:3.128.0') {
      transitive = false
    }
    implementation ('org.eclipse.platform:org.eclipse.jface:3.35.100') {
      transitive = false
    }
    implementation ('org.eclipse.platform:org.eclipse.ui.workbench:3.134.0'){
      transitive = false
    }
    implementation('org.eclipse.platform:org.eclipse.ui:3.207.0') {
        transitive = false
    }
    implementation('org.eclipse.platform:org.eclipse.ui.console:3.14.200') {
        transitive = false
    }
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'commons-io:commons-io:2.18.0'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = 'net.sf.ecl1.utilities.preferences.standalone.PreferencesApp'
}

tasks.register("runSymlinkCreator", JavaExec) {
    description = "Creates a symlink to a given path in the workspace folder."
    group = "util"
    mainClass.set("net.sf.ecl1.utilities.standalone.SymlinkCreator")
    classpath = sourceSets.main.runtimeClasspath

    println "symlinkTarget: ${project.hasProperty('symlinkTarget') ? project.symlinkTarget : 'not set'}"

    args project.hasProperty('symlinkTarget') ? project.symlinkTarget.split(',') : []
}

tasks.register("addWebappsSymlink", JavaExec) {
    description = "Creates a symlink to Webapps in the workspace folder."
    group = "util"
    mainClass.set("net.sf.ecl1.utilities.standalone.SymlinkCreator")
    classpath = sourceSets.main.runtimeClasspath
    args = ['../webapps']
}

tasks.register('copyVSCodeTasks', JavaExec) {
    description = "Copy tasks.json into VSCode workspace."
    group = "util"
    mainClass.set("net.sf.ecl1.utilities.standalone.vscode.CopyTasks")
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('toggleEcl1Visibility', JavaExec) {
    description = "Toggle ecl1 visibility in VSCode workspace."
    group = "util"
    mainClass.set("net.sf.ecl1.utilities.standalone.vscode.ToggleEcl1VisibilityInWorkspace")
    classpath = sourceSets.main.runtimeClasspath
}
